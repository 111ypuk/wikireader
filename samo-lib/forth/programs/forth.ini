.( autostart )
decimal

lcd-cls

10 constant max-items
8 1+ 3 + 1+ constant item-length  \ 8.3\0


max-items item-length * constant items-size

create items items-size allot

variable menu-count

: read-items ( -- )
    items items-size 0 fill

    lcd-cls
    s" Forth programs" lcd-type lcd-cr lcd-cr

    s" /" open-directory ?dup
    if  cr ." open-directory error = "
        dec. drop exit
    then
    >r    \ save dirid
    begin
        here 256 r@ read-directory ?dup
        if  cr ." directory read error = "
            dec. drop
            r> close-directory drop exit
        then
        dup
    while
            here swap ( c-addr u )
            2dup s" .4TH" search if
                4 = if
                    drop 2dup
                    menu-count @ item-length * items + \ src count dest
                    swap cmove
                    4 -
                    bl lcd-emit
                    lcd-type lcd-cr
                    1 menu-count +!
                else
                    drop 2drop
                then
            else
                2drop 2drop
            then
    repeat
    drop
    r> close-directory drop
;

: menu-select ( -- n)
    begin
        ctp-pos? if
            ctp-pos dup 0<
            if
                2drop 0
            else
                nip font-height / 1+
            then
            exit
        then

        P6_P6D p@ $07 and
        case
            $02 of   \ left button
                -2
                exit
            endof
            $04 of   \ centre button
                -4
                exit
            endof
            $01 of   \ right button
                -1
                exit
            endof
        endcase
    again
;


: run-program ( c-addr u -- )
    lcd-cls s" Loading: " lcd-type
    2dup lcd-type
    included
    quit
;

variable menu-cursor

: menu ( -- )
    read-items
    begin
        menu-select dup 2 >
        if
            dup 3 - menu-count @ < if
                menu-cursor @ ?dup if
                    1- font-height * font-height lcd-invert-lines
                then
                dup menu-cursor !
                1- font-height * font-height lcd-invert-lines
                0
            then
        else
            menu-cursor @ ?dup if
                3 -  item-length * items +
                dup
                begin
                    dup c@
                while
                        1+
                repeat
                over - ['] run-program catch
                cold \ restart menu
            then
            0 menu-cursor !
        then
        -1 =
    until
    -1 throw \ will abort menu and return to command prompt
;

.( start menu - press Right Key to abort )
menu

\ ensure restart will occur if any program exits
cold
cold
cold
cold

.( autostart - exiting )
decimal
