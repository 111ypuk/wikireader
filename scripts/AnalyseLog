#!/bin/sh
# simple script to check the progress

gawk -- '
BEGIN {
  index_count = 0
  index_delta = 0
  index_total = 0
  delete parse_count
  delete parse_delta
  delete parse_total
  delete render_count
  delete render_delta
  delete render_total
}

END {
    if (0 != index_delta) {
        printf("Index: %14d %8.2fs per %d\n", index_count, (index_total / (index_count / index_delta)), index_delta)
    } else {
        print "Index: Not Started"
    }

    if (0 != length(parse_delta)) {
        for (i in parse_delta) {
            printf("Parse[%2d]: %10d %8.2fs per %d\n", i, parse_count[i], (parse_total[i] / (parse_count[i] / parse_delta[i])), parse_delta[i])
        }
    } else {
        print "Parse: Not Started"
    }

    if (0 != length(render_delta)) {
        for (i in render_delta) {
            printf("Render[%2d]: %10d %8.2fs per %d\n", i, render_count[i], (render_total[i] / (render_count[i] / render_delta[i])), render_delta[i])
        }
    } else {
        print "Render: Not Started"
    }
}

/^Index:/ {
  index_total += $2
  n = $3
  index_delta = n - index_count
  index_count = n
}

/^Parse\[/ {
  p1 = index($0, "-")
  p2 = index($0, ".")
  if (p1 > 0 && p2 > 0) {
      i = substr($0, p1 + 1, p2 - p1 - 1)
      parse_total[i] += $2
      n = $3
      parse_delta[i] = n - parse_count[i]
      parse_count[i] = n
  }
}

/^Render\[/ {
  p1 = index($0, "[")
  p2 = index($0, "]")
  if (p1 > 0 && p2 > 0) {
      i = substr($0, p1 + 1, p2 - p1 - 1)
      render_total[i] += $2
      n = $3
      render_delta[i] = n - render_count[i]
      render_count[i] = n
  }
}


' "$@"
